apply plugin: 'com.android.application'

android {
	compileSdkVersion rootProject.ext.CompileSdk

	namespace 'website.leifs.delta.adaptive'

	defaultConfig {
		applicationId "website.leifs.delta.adaptive"
		minSdkVersion rootProject.ext.MinSdk
		targetSdkVersion rootProject.ext.TargetSdk
		versionCode 180001
		versionName "0.0.1"
		multiDexEnabled true

		// Naming app bundles
		setArchivesBaseName("${parent.name}-v${versionCode}")
	}

	bundle {
		language {
			enableSplit false
		}
	}

	buildTypes {
		release {
			debuggable false
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	flavorDimensions "variant"
	productFlavors {
		play {
			dimension "variant"
			resValue "string", "app_name", "Delta Adaptive"
			matchingFallbacks = ["play"]
		}
		foss {
			dimension "variant"
			applicationIdSuffix '.foss'
			resValue "string", "app_name", "Delta Adaptive"
			matchingFallbacks = ["foss"]
		}
	}

	sourceSets {
		play { java.srcDirs = ['src/play/java/'] }
		foss { java.srcDirs = ['src/foss/java/'] }

	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_11
		targetCompatibility JavaVersion.VERSION_11
	}
}

dependencies {
	/*
     * CandyBar Material Dashboard Library
     *
     * TODO: How to update the dashboard?!
     * Check this update guide for every version
     *      - https://github.com/zixpo/candybar-sample/wiki/Updating-the-dashboard
     * If you've missed some versions you've to also follow update guide for those versions
     */

	playImplementation 'com.github.zixpo:candybar:3.17.5'
	fossImplementation 'com.github.Donnnno:candybar-foss:3.17.3'
}
task copyXMLs{
	['appfilter.xml', 'drawable.xml'].each {file ->
		copy {
			from "$projectDir/src/main/res/xml/$file"
			into "$projectDir/src/main/assets/"
		}
	}
}
tasks.whenTaskAdded { task ->
	if (task.name == 'assembleDebug' || task.name == 'assembleRelease') {
		task.dependsOn copyXMLs
	}
}
